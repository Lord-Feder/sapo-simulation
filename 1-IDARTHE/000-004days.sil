//HEADER
problem: reachability;
iterations:400;
//iterations:4;

//SYMBOL DEFINITION

var I in [0.00000316666,0.0000035]; //0.00000333333
var D in [0.000000316666,0.00000035]; //0.000000333333
var A in [0.0000000158334,0.0000000175]; //0.0000000166666
var R in [0.0000000316666,0.000000035]; //0.0000000333333
var T in [0,0]; //no one yet
var H in [0,0]; //no one yet
var E in [0,0]; //no one yet



param alpha in [0.422,0.570];
param beta in [0.0057,0.011];
param gamma in [0.285,0.456];
param delta in [0.0057,0.011];

const epsilon = 0.171;
const zeta = 0.125;
const eta = 0.125;
const theta = 0.371;
const kappa = 0.017;
const lambda = 0.034;
const mu = 0.017;
const nu = 0.027;
const xi = 0.017;
const rho = 0.034;
const sigma = 0.017;
const tau = 0.01;

const step = 0.01;

dynamic (I) = I + ((1-I-D-A-R-T-H-E)*(alpha*I + beta*D + gamma*A + delta*R) -(epsilon + zeta + lambda)*I)*step;
dynamic (D) = D + (epsilon*I -(eta + rho)*D)*step;
dynamic (A) = A + (zeta*I -(theta + mu + kappa)*A)*step;
dynamic (R) = R + (eta*D + theta*A -(nu + xi)*R)*step;
dynamic (T) = T + (mu*A + nu*R -(sigma + tau)*T)*step;
dynamic (H) = H + (lambda*I + rho*D + kappa*A + xi*R + sigma*T)*step;
dynamic (E) = E + (tau*T)*step;
